=======================================================================================
Carbon flow model in plankton community during spring in inner (western) Beagle Channel

Time: spring
Space: inner BC, mixed layer

Units:  fluxes: mg C/m3/d
        stocks: mg C/m3 (converted from gAFDW)
Depth:  mixed layer (0-40 m)

Tomás I. Marina, Karline Soetaert, Dick van Oevelen
=======================================================================================

!--------------------------------------------------------------------------------------------------!									
!                                                                                                  !									
!                                     PARAMETERS & VARIABLES                                       !									
!                                                                                                  !									
!--------------------------------------------------------------------------------------------------!

###### PARAMETERS

!!! STOCKS
!stwDET         	=
stDOC         	=   1090.0    {mg C/m3}
stBAC           =   8.3       {mg C/m3}
stPHY         	=   105.866   {mg C/m3}
stHNF           =   80.2069   {mg C/m3}
stMIZ           =   7.4513    {mg C/m3}
stMEZ           =   76.780    {mg C/m3}
stICH         	=   0.2136    {mg C/m3}
stGRI         	=   438.87    {mg C/m2}
!stFPL         	  =

!!! FLOW MEASUREMENTS
meanProdPHY     =   51.19         {mg C/m3/d}
sdProdPHY       =   5.2           {mg C/m3/d}
minProdPHY      =   meanProdPHY - sdProdPHY   {mg C/m3/d}
maxProdPHY      =   meanProdPHY + sdProdPHY   {mg C/m3/d}

!!! METABOLIC RATES (ingestion, respiration, assimilation)
!-- Phytoplankton
meanRespPHY     =   24.48         {mg C/m3/d}
sdRespPHY       =   0.64          {mg C/m3/d}
minRespPHY      =   meanRespPHY - sdRespPHY   {mg C/m3/d}
maxRespPHY      =   meanRespPHY + sdRespPHY   {mg C/m3/d}

!-- Heterotrophic nanoflagellates (from Gonzalez et al. 2016)
minUptHNF       = 0.1
maxUptHNF       = 0.2

!-- Grimothea gregaria (from Romero et al. 2006)
minUptGRI       =   0.015         {-}
maxUptGRI       =   0.05          {-}
minAEGRI        =   0.945         {-}
maxAEGRI        =   0.975         {-}


!!! PROPORTIONAL CONTRIBUTIONS (from Bruno et al. 2023 & Presta et al. 2023)
!-- export to Detritus in benthos
minwDETtoeDET   =   0.03
maxwDETtoeDET   =   0.27
minPHYtoeDET    =   0.01
maxPHYtoeDET    =   0.17

!-- to Detritus in water column
minPHYtowDET    =   0.45
maxPHYtowDET    =   0.75
miniMACtowDET   =   0.00            ! mean 'blades', 'stipes', 'holdfasts'
maxiMACtowDET   =   0.13            ! mean 'blades', 'stipes', 'holdfasts'
miniTC3towDET   =   0.05
maxiTC3towDET   =   0.25

!-- to MicroZooplankton
minwDETtoMIZ    =   0.62
maxwDETtoMIZ    =   0.90
minPHYtoMIZ     =   0.03
maxPHYtoMIZ     =   0.15

!-- to MesoZooplankton
minMIZtoMEZ     =   0.14
maxMIZtoMEZ     =   0.34

!-- to Ichthyoplankton
minwDETtoICH    =   0.00
maxwDETtoICH    =   0.08
minPHYtoICH     =   0.00
maxPHYtoICH     =   0.10

!-- to Grimothea gregaria
minMIZtoGRI     =   0.07
maxMIZtoGRI     =   0.39
minMEZtoGRI     =   0.006
maxMEZtoGRI     =   0.13
minICHtoGRI     =   0.00
maxICHtoGRI     =   0.11

###### END PARAMETERS

###### VARIABLES

!-- Phytoplankton
PHYResp		      = PHY   ->  eDIC						
PHYUpt		      =	eDIC  ->  PHY			
PHYProd		      =	PHYUpt	-	PHYResp

!-- Heterotrophic nanoflagellates
HNFUptBAC       = BAC   ->  HNF

!-- MesoZooplankton
MEZExp          = MEZ   ->  eHTL

!-- Ichthyoplankton
ICHExp          = ICH   ->  eHTL

!-- Grimothea gregaria
GRIUpt          = flowto(GRI)
GRIwDETProd     = GRI   ->  wDET
GRIAs           = GRIUpt  - GRIwDETProd
GRIExp          = GRI   ->  eHTL

!-- Fish pelagic
FPLExp          = FPL   ->  eHTL

!-- Autochtonous vs Alochthonous (from Bruno et al. 2023)
AlotowDET       = iTC3  ->  wDET
AuttowDET       = iMAC  ->  wDET + PHY  ->  wDET

!!! TOTALS
HTLExp          = MEZExp + ICHExp + GRIExp + FPLExp


###### END VARIABLES

!--------------------------------------------------------------------------------------------------!									
!                                                                                                  !									
!                                        SYSTEM DECLARATION                                        !									
!                                  (COMPARTMENTS, EXTERNALS, FLOWS)                                !									
!                                                                                                  !									
!--------------------------------------------------------------------------------------------------!

! I didn't consider detritus in sediment as input (to wDET, DOC, and food for MIZ, MEZ, ICH, GRI), only as export to benthos.

###### COMPARTMENTS

wDET    ! Detritus in water column
DOC     ! Dissolved Organic Carbon
BAC     ! Bacteria
PHY     ! Phytoplankton (mainly Diatoms)
HNF     ! Heterotrophic nanoflagellates
MIZ     ! Microzooplankton (Copepoda & Cirripedia nauplii, 67–200 μm)
MEZ     ! Mesozooplankton (Copepoda copepodites & adults, Appendicularia, Echinoderm larvae, 200–20000 μm)
ICH     ! Ichthyoplankton (all fish spp)
GRI     ! Grimothea gregaria (Decapoda)
FPL     ! Fish pelagic (Sprattus fuegensis)

###### END COMPARTMENTS

###### EXTERNALS

iTC3    ! input of terrestrial plants
iMAC    ! input of detached macroalgae
eDIC    ! export of Dissolved Inorganic Carbon
eDET    ! export of detritus to sediments
eHTL    ! export to Higher Tropic Levels (seabirds & marine mammals)

###### END EXTERNALS

###### FLOWS

!-- External
iTC3    ->  wDET    ! terrestrial input
iTC3    ->  DOC     ! terrestrial input
iMAC    ->  wDET    ! macroalgae input
eDIC    ->  PHY     ! uptake

!-- Detritus
wDET    ->  eDET    ! export to benthos
wDET    ->  DOC     ! hydrolisis
wDET    ->  BAC     ! consumption
wDET    ->  GRI     ! consumption
wDET    ->  MIZ     ! consumption
wDET    ->  MEZ     ! consumption
wDET    ->  ICH     ! consumption

!-- Dissolved Organic Carbon
DOC     ->  BAC     ! uptake

!-- Bacteria
BAC     ->  DOC     ! DOC production
BAC     ->  eDIC    ! respiration
BAC     ->  HNF     ! consumption

!-- Phytoplankton
PHY     ->  eDIC    ! respiration
PHY     ->  eDET    ! natural mortality & deposition
PHY     ->  wDET    ! natural mortality=phytodetritus
PHY     ->  DOC     ! exudation
PHY     ->  MIZ     ! consumption
PHY     ->  MEZ     ! consumption
PHY     ->  ICH     ! consumption
PHY     ->  GRI     ! consumption
PHY     ->  FPL     ! consumption

!-- Heterotrophic nanoflagellates
HNF     ->  eDIC    ! respiration
HNF     ->  wDET    ! natural mortality
HNF     ->  MIZ     ! consumption

!-- MicroZooplankton
MIZ     ->  eDIC    ! respiration
MIZ     ->  wDET    ! natural mortality
MIZ     ->  MEZ     ! consumption
MIZ     ->  ICH     ! consumption
MIZ     ->  GRI     ! consumption
MIZ     ->  FPL     ! consumption

!-- MesoZooplankton
MEZ     ->  eDIC    ! respiration
MEZ     ->  wDET    ! natural mortality
MEZ     ->  MEZ     ! consumption
MEZ     ->  ICH     ! consumption
MEZ     ->  GRI     ! consumption
MEZ     ->  FPL     ! consumption
MEZ     ->  eHTL    ! consumption by seabirds

!-- Ichthyoplankton
ICH     ->  eDIC    ! respiration
ICH     ->  wDET    ! natural mortality
ICH     ->  GRI     ! consumption
ICH     ->  FPL     ! consumption
ICH     ->  eHTL    ! consumption by seabirds

!-- Grimothea gregaria
GRI     ->  eDIC    ! respiration
GRI     ->  wDET    ! natural mortality
GRI     ->  eHTL    ! consumption by seabirds & marine mammals

!-- Fish pelagic
FPL     ->  eDIC    ! respiration
FPL     ->  wDET    ! natural mortality
FPL     ->  eHTL    ! consumption by seabirds & marine mammals

###### END FLOWS

!--------------------------------------------------------------------------------------------------!									
!                                                                                                  !									
!                                         DATA DECLARATION                                         !									
!                                (RATES, EQUALITIES, INEQUALITIES)                                 !									
!                                                                                                  !									
!--------------------------------------------------------------------------------------------------!

###### INEQUALITIES

!!! PROPORTIONAL CONTRIBUTIONS
!-- to Detritus in water column
PHY   ->  wDET      = [minPHYtowDET, maxPHYtowDET]*flowto(wDET)
iMAC  ->  wDET      = [miniMACtowDET, maxiMACtowDET]*flowto(wDET)
iTC3  ->  wDET      = [miniTC3towDET, maxiTC3towDET]*flowto(wDET)

!-- export to Detritus in benthos
wDET  ->  eDET      = [minwDETtoeDET, maxwDETtoeDET]*flowto(eDET)
PHY   ->  eDET      = [minPHYtoeDET, maxPHYtoeDET]*flowto(eDET)

!-- to MicroZooplankton
wDET  ->  MIZ       = [minwDETtoMIZ, maxwDETtoMIZ]*flowto(MIZ)
PHY   ->  MIZ       = [minPHYtoMIZ, maxPHYtoMIZ]*flowto(MIZ)

!-- to MesoZooplankton
MIZ   ->  MEZ       = [minMIZtoMEZ, maxMIZtoMEZ]*flowto(MEZ)

!-- to Ichthyoplankton
wDET  ->  ICH       = [minwDETtoICH, maxwDETtoICH]*flowto(ICH)
PHY   ->  ICH       = [minPHYtoICH, maxPHYtoICH]*flowto(ICH)

!-- to Grimothea gregaria
MIZ   ->  GRI       = [minMIZtoGRI, maxMIZtoGRI]*flowto(GRI)
MEZ   ->  GRI       = [minMEZtoGRI, maxMEZtoGRI]*flowto(GRI)
ICH   ->  GRI       = [minICHtoGRI, maxICHtoGRI]*flowto(GRI)

!-- Autochthonous vs Alochthonous
AuttowDET           > AlotowDET


!!! METABOLISM
!-- Phytoplankton
PHYProd		=	[minProdPHY, maxProdPHY]
PHYResp		= [minRespPHY, maxRespPHY]*PHYProd

!-- Heterotrophic nanoflagellates
HNFUptBAC = [minUptHNF, maxUptHNF]*stHNF

!-- Grimothea gregaria
GRIUpt    = [minUptGRI, maxUptGRI]*stGRI
GRIAs     = [minAEGRI, maxAEGRI]*GRIUpt

###### END INEQUALITIES


!!! DOUBTS
! Implementation of growth rates: minGrowthLat = 0.00035	{/d}. I should know the length and weight of succesive days, right? YES
! m2 to m3. Have to divide it by depth?
! Convert μlO2 /h/g to mg C /m3/d. Of Grimothea gregaria from Romero et al. 2006

! Added assimilation efficiencies: GRI, 
! Add respiration for Grimothea gregaria
! Look for metabolism (production, assimilation, respiration) of Calanoid/Cyclopoid copepod (MEZ)
! Add Heterotrophic nanoflagellates (HNF), stock (Malits2023) & bacterivory rate (Gonzalez2016)
! Transform ingestion rate of HNF (to bacteria): 20-37 pgC HNF−1 d−1 to mgC m-3 d-1. Multiple by abundance (cell mL-1)
! Vertical POC flux (flowto(eDET)): 
